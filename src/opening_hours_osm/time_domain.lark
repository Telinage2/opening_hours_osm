// Source: https://wiki.openstreetmap.org/wiki/Key:opening_hours/specification
// Specification version 0.7.4

?start: time_domain

// Constants
WDAY : "Mo" | "Tu" | "We" | "Th" | "Fr" | "Sa" | "Su"
WEEKNUM : /([1-4]\d)|(5[0-3])|(0?[1-9])/
MONTH : "Jan" | "Feb" | "Mar" | "Apr" | "May" | "Jun" | "Jul" | "Aug" | "Sep" | "Oct" | "Nov" | "Dec"
DAYNUM : /([12]\d)|(3[01])|(0?[1-9])/
MDAY : DAYNUM "."
HOUR : /(1\d)|(2[0-4])|(0?\d)/
EXTENDED_HOUR : /([1-3]\d)|(4[0-8])|(0?\d)/
MINUTE : /[0-5]\d/
NTH : /[1-5]/
ALWAYS_OPEN : "24/7"
OPEN : "open" | "on"
CLOSED : "closed" | "off"
UNKNOWN : "unknown"
WEEK : "week"
DAYS : "day" "s"?
POSITIVE_NUMBER : /\d+/
YEAR : /([1-9][\d]{3})+/
PLUS : "+"
MINUS : "-"
_SPACE : " "
PUBLIC_HOLIDAY : "PH"
SCHOOL_HOLIDAY : "SH"
EASTER : "easter"
DAWN: "dawn"
SUNRISE: "sunrise"
SUNSET: "sunset"
DUSK: "dusk"

hour_minutes : HOUR ":" MINUTE
extended_hour_minutes : EXTENDED_HOUR ":" MINUTE
plus_or_minus : PLUS | MINUS

comment : "\"" (CPART | CESCAPE)* "\""
CPART : /[^"\\]+/
CESCAPE : /\\[\\"]/

// Definition
time_domain : rule_sequence (any_rule_separator rule_sequence)*
rule_sequence : selector_sequence _SPACE? rule_modifier?

// Rule separators
// Rules are evaluated from left to right. If a normal rule that specifies open hours applies to a given day, then these hours are the only hours that are open that day.
// If an additional rule applies to a given day, then these hours are open that day, in addition to all the hours specified by earlier rules.
any_rule_separator : normal_rule_separator | additional_rule_separator | fallback_rule_separator
normal_rule_separator : _SPACE? ";" _SPACE?
additional_rule_separator : "," _SPACE
// The rule which follows this separator (token) will match every time frame not covered by previous rules.
fallback_rule_separator : _SPACE? "||" _SPACE

// Rule modifiers
rule_modifier : rule_kind (_SPACE? comment)? | comment
rule_kind : OPEN | CLOSED | UNKNOWN

// Selectors
selector_sequence : ALWAYS_OPEN | wide_range_selectors small_range_selectors?
// The specified grammar is ambiguous: `year_selector` can contain a single year which could also be the optional prefix of
// `monthday_selector`. In such a case `monthday_selector` will be favored.
wide_range_selectors : comment ":" | monthday_selector week_selector? separator_for_readability? | year_selector? monthday_selector? week_selector? separator_for_readability?
small_range_selectors : weekday_selector _SPACE time_selector | weekday_selector | time_selector
separator_for_readability : ":" _SPACE? | _SPACE

// Time selector
time_selector : timespan ("," timespan)*
// timespan : time ("-" extended_time)? (PLUS | repeated_time)?
timespan : time _SPACE? "-" extended_time _SPACE? "/" _SPACE? hour_minutes
    | time _SPACE? "-" extended_time _SPACE? "/" _SPACE? MINUTE
    | time _SPACE? "-" _SPACE? extended_time PLUS?
    | time PLUS

time : hour_minutes | variable_time
extended_time : extended_hour_minutes | variable_time
variable_time : "(" event plus_or_minus hour_minutes ")" | event
event : DAWN | SUNRISE | SUNSET | DUSK

// Weekday selector
// weekday_selector : weekday_sequence | holiday_sequence | holiday_sequence "," weekday_sequence | weekday_sequence "," holiday_sequence | holiday_sequence _SPACE weekday_sequence
weekday_selector : holiday_sequence ( ( "," | _SPACE ) weekday_sequence )?
    | weekday_sequence ( ( "," | _SPACE ) holiday_sequence )?

weekday_sequence : weekday_range ("," weekday_range)*
weekday_range : WDAY ( ("-" WDAY)? | "[" nth_entry ("," nth_entry)* "]" day_offset? )
holiday_sequence : holiday ("," holiday)*
holiday : PUBLIC_HOLIDAY day_offset? | SCHOOL_HOLIDAY
nth_entry : NTH | NTH "-" NTH | MINUS NTH
day_offset : _SPACE plus_or_minus POSITIVE_NUMBER _SPACE DAYS

// Week selector
week_selector : separator_for_readability? WEEK _SPACE? week ("," week)*
week : WEEKNUM ( "-" WEEKNUM week_step? )?
// This notation is borrowed from cron and enables to express "in even weeks" or "on odd days".
week_step : "/" POSITIVE_NUMBER

// Month selector
monthday_selector : monthday_range ("," monthday_range)*
monthday_range : monthday_range_from | YEAR? MONTH ("-" MONTH)?
monthday_range_from : date_from date_offset? (_SPACE? "-" _SPACE? date_to date_offset? | PLUS?)
// Given any calendar day, the notation +Su selects the first Sunday after this calendar day, the notation -Su selects the last Sunday before this calendar day.
date_offset : plus_or_minus WDAY day_offset? | day_offset
date_from : (YEAR _SPACE?)? MONTH _SPACE? DAYNUM | YEAR? _SPACE? variable_date
date_to : date_from | DAYNUM
variable_date : EASTER

// Year selector
year_selector : year_range ("," year_range)*
year_range : YEAR ( ("-" YEAR year_step? ) | PLUS )?
year_step : "/" POSITIVE_NUMBER
