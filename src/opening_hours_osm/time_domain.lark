// Source: https://wiki.openstreetmap.org/wiki/Key:opening_hours/specification
// Specification version 0.7.4

%ignore " "
?start: time_domain

// Constants
WDAY : "Mo" | "Tu" | "We" | "Th" | "Fr" | "Sa" | "Su"
WEEKNUM : /([1-4]\d)|(5[0-3])|(0?\d)/
MONTH : "Jan" | "Feb" | "Mar" | "Apr" | "May" | "Jun" | "Jul" | "Aug" | "Sep" | "Oct" | "Nov" | "Dec"
DAYNUM : /([12]\d)|(3[01])|(0?\d)/
MDAY : DAYNUM "."
HOUR : /(1\d)|(2[0-4])|(0?\d)/
EXTENDED_HOUR : /([1-3]\d)|(4[0-8])|(0?\d)/
MINUTE : /[0-5]\d/
NTH : /[1-5]/
ALWAYS_OPEN : "24/7"
OPEN : "open" | "on"
CLOSED : "closed" | "off"
UNKNOWN : "unknown"
WEEK : "week"
DAYS : "day" "s"?
POSITIVE_NUMBER : /\d+/
YEAR : /([1-9][\d]{3})+/
COMMENT : /"(?:\\.|[^"\\])*"/ // /"[^"]*"/
PLUS : "+"
MINUS : "-"
SPACE : " "
PUBLIC_HOLIDAY : "PH"
SCHOOL_HOLIDAY : "SH"
EASTER : "easter"
DAWN: "dawn"
SUNRISE: "sunrise"
SUNSET: "sunset"
DUSK: "dusk"

hour_minutes : HOUR ":" MINUTE
extended_hour_minutes : EXTENDED_HOUR ":" MINUTE
plus_or_minus : PLUS | MINUS


// Definition
time_domain : rule_sequence (any_rule_separator rule_sequence)*
rule_sequence : selector_sequence rule_modifier? | rule_modifier

// Rule separators
// Rules are evaluated from left to right. If a normal rule that specifies open hours applies to a given day, then these hours are the only hours that are open that day.
// If an additional rule applies to a given day, then these hours are open that day, in addition to all the hours specified by earlier rules.
any_rule_separator : normal_rule_separator | additional_rule_separator | fallback_rule_separator
normal_rule_separator : ";" SPACE
additional_rule_separator : "," SPACE
// The rule which follows this separator (token) will match every time frame not covered by previous rules.
fallback_rule_separator : SPACE "||" SPACE

// Rule modifiers
rule_modifier : rule_kind COMMENT? | COMMENT
rule_kind : OPEN | CLOSED | UNKNOWN

// Selectors
selector_sequence : ALWAYS_OPEN | wide_range_selectors small_range_selectors
wide_range_selectors : year_selector? monthday_selector? week_selector? separator_for_readability? | COMMENT separator_for_readability
small_range_selectors : weekday_selector? time_selector?
separator_for_readability : ":"

// Time selector
time_selector : timespan ("," timespan)*
timespan : time ("-" extended_time)? (PLUS | repeated_time)?
// 10:00-16:00/90 and 10:00-16:00/01:30 are evaluated as "from ten am to four pm every 1.5 hours"
repeated_time : "/" (POSITIVE_NUMBER | hour_minutes)
time : hour_minutes | variable_time
extended_time : extended_hour_minutes | variable_time
variable_time : "(" event plus_or_minus hour_minutes ")" | event
event : DAWN | SUNRISE | SUNSET | DUSK

// Weekday selector
weekday_selector : weekday_sequence | holiday_sequence | holiday_sequence "," weekday_sequence | weekday_sequence "," holiday_sequence | holiday_sequence SPACE weekday_sequence
weekday_sequence : weekday_range ("," weekday_range)*
weekday_range : WDAY ( ("-" WDAY)? | "[" nth_entry ("," nth_entry)* "]" day_offset? )
holiday_sequence : holiday ("," holiday)*
holiday : PUBLIC_HOLIDAY day_offset? | SCHOOL_HOLIDAY
nth_entry : NTH | NTH "-" NTH | MINUS NTH
day_offset : SPACE plus_or_minus POSITIVE_NUMBER SPACE DAYS

// Week selector
week_selector : WEEK week ("," week)*
week : WEEKNUM ( "-" WEEKNUM week_step? )?
// This notation is borrowed from cron and enables to express "in even weeks" or "on odd days".
week_step : "/" POSITIVE_NUMBER

// Month selector
monthday_selector : monthday_range ("," monthday_range)*
monthday_range : YEAR? MONTH ("-" MONTH)? | monthday_range_from
monthday_range_from : date_from ( date_offset (PLUS | ("-" date_to day_offset?))? )?
// Given any calendar day, the notation +Su selects the first Sunday after this calendar day, the notation -Su selects the last Sunday before this calendar day.
date_offset : (plus_or_minus WDAY)? day_offset?
// date_from : YEAR? ( ( MONTH (DAYNUM | ( WDAY (MINUS? NTH)? )) ) | variable_date )
date_from : YEAR? ( MONTH  DAYNUM | variable_date )
date_to : date_from | DAYNUM
variable_date : EASTER

// Year selector
year_selector : year_range ("," year_range)*
year_range : YEAR ( ("-" YEAR year_step? ) | PLUS )?
year_step : "/" POSITIVE_NUMBER
